apply plugin: 'com.android.library'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.3"
    com.android.ddmlib.DdmPreferences.setLogLevel("verbose")
    com.android.ddmlib.DdmPreferences.setTimeOut(10 * 60000)

    defaultPublishConfig "release"
    publishNonDefault true

    defaultConfig {
        minSdkVersion 12
        targetSdkVersion 23
        versionCode = 2007
        versionName = "2.0.7"

        setProperty("archivesBaseName", "unity-ads")

        buildConfigField 'String', 'WEBVIEW_BRANCH', getPropertyStringWithDefaultValue('WEBVIEW_BRANCH', '"' + versionName + '"')

        testBuildType "coverage"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments disableAnalytics: 'true'
        // Won't work yet, see: https://code.google.com/p/android/issues/detail?id=188241
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes  {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt')
            consumerProguardFiles 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            testCoverageEnabled false
            consumerProguardFiles 'proguard-rules.pro'
            buildConfigField 'String', 'WEBVIEW_BRANCH', getPropertyStringWithDefaultValue('WEBVIEW_BRANCH', '"master"')
        }
        coverage {
            testCoverageEnabled true
            consumerProguardFiles 'proguard-rules.pro'
            signingConfig signingConfigs.debug
            buildConfigField 'String', 'WEBVIEW_BRANCH', getPropertyStringWithDefaultValue('WEBVIEW_BRANCH', '"master"')
            sonarqube {
                properties {
                    property "sonar.projectKey", "unity-ads-android"
                    property "sonar.projectName", "UnityAds SDK Android"
                    property "sonar.projectVersion", "2.0.0"
                    property "sonar.sourceEncoding", "UTF-8"
                    property "sonar.host.url", "${System.env.SONAR_HOST}"
                    property "sonar.language", "java"
                    property "sonar.sources", "lib/src"
                    property "sonar.java.binaries", "lib/build/intermediates/classes/coverage/"
                    property "sonar.java.libraries", "${System.env.ANDROID_SDK_ROOT}/platforms/android-23/android.jar"
                    property "sonar.projectBaseDir", "."
                    property "sonar.junit.reportsPath", "lib/build/outputs/androidTest-results/connected"
                    property "sonar.jacoco.reportPath", "lib/build/outputs/code-coverage/connected/coverage.ec"
                }
            }
        }
    }
}

dependencies {
    testCompile 'junit:junit:4.12'
    androidTestCompile 'org.mockito:mockito-core:1.10.19'
    androidTestCompile 'com.android.support.test:runner:0.4'
    androidTestCompile 'com.android.support.test:rules:0.4'
    compile 'com.google.code.gson:gson:2.7'
    compile 'com.google.guava:guava:19.0'
}

android.testVariants.all { variant ->
    variant.outputs.each { output ->
        output.outputFile = new File(output.outputFile.parent, "androidTest.apk")
    }
}

android.libraryVariants.all { variant ->
    task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
        description "Generates Javadoc for $variant.name."
        source = variant.javaCompile.source
        ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
        classpath = files(variant.javaCompile.classpath.files) + files(ext.androidJar)
        options {
            links "http://docs.oracle.com/javase/7/docs/api/"
            linksOffline "http://developer.android.com/reference", "${android.sdkDirectory}/docs/reference"
        }
        exclude '**/R.java'
        destinationDir = file("../javadoc/")
    }
}

def getPropertyStringWithDefaultValue(String key, String defaultValue) {
    def value = project.getProperties().get(key);
    // Ensure that string is quoted
    if ((value != null) && (!(value ==~ /^".*"$/))) {
        println "Value for key '$key' is not quoted, adding quotes to '$value'";
        value = "\"$value\"";
    }
    return value != null ? value : defaultValue;
}

def webAssets = 'src/main/assets/web'

task cleanData(type: Delete) {
    delete webAssets
}

task copyFiles(type: Copy) {
    from '../ads/build'
    into webAssets
}

copyFiles.dependsOn(cleanData)
preBuild.dependsOn(copyFiles)